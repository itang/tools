# justfile
## ref link: https://github.com/casey/just/blob/master/examples/kitchen-sink.just

set shell := ["nu", "-c"]

main_js_file := "lib/es6/src/Main.res.mjs"


default:
  just --list

prepare:
  pnpm install

alias setup := prepare

build-res:
  pnpm run res:build

run-node *args: build-res
  node {{main_js_file}} {{args}}

run-deno *args: build-res
  deno run -A {{main_js_file}} {{args}}

run-bun *args: build-res
  bun run {{main_js_file}} {{args}}

alias run := run-node

dev:
  pnpm run dev

all: prepare dev

compile-deno: build-res
  deno compile -A --unstable -o dist/iurl {{main_js_file}}
  ls -l dist

compile-bun: build-res
  bun build --compile --outfile=dist/iurl {{main_js_file}}

compile: compile-deno

[windows]
install:compile-bun
  cp dist/iurl.exe D:/dev-env/bin/iurl-res.exe

bench-runtime:
  hyperfine "bun run {{main_js_file}}" "node {{main_js_file}}" "deno run -A {{main_js_file}}"

fmt:
  pnpm run res:fmt
