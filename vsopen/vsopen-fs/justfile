# justfile
## ref link: https://github.com/casey/just/blob/master/examples/kitchen-sink.just

set shell := ["nu", "-c"]

default:
  just --list


prepare:
  dotnet tool restore


run *args:
  dotnet run -- {{args}}

run-no-build *args:
  dotnet run --no-build -- {{args}}


dev *args:
  dotnet watch run -- {{args}}


build mode="Release":
  dotnet build -c {{mode}}


[windows]
publish:
  dotnet publish -r win-x64 -c Release
  ls bin\Release\net8.0\win-x64\publish\

[linux]
publish:
  dotnet publish -r linux-x64 -c Release


[windows]
install: publish
    cp bin\Release\net8.0\win-x64\publish\*.exe D:/dev-env/bin/

[linux]
install: publish
  cp bin/Release/net8.0/linux-x64/publish/* /home/itang/.local/bin/

fmt:
  dotnet fantomas .
